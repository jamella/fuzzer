// one line comment
/* this comment can be a single line */
/*
	or multiple
	lines
	long
	and can be inlined too
*/
/*

Token = "Definition" 123 // Everything is concatenated by default
AnotherToken = Token // AnotherToken uses the token "Token"
MultiLineToken = "a", // token definitions can have multiple lines if at the end of a line is a ","
                 "b",
                 "c" // no "," so the definition ends here

Umläüte = "can be used since this files have to be in utf8"
Quoting = "\"this is quoted\""

Alternation = 1 | 2 | 3
SameAlternationAsShortage = [123]
Grouping = ("old" | "new") "letter"

Optional = "i am not optional" ?{"but hey i am optional!"}
RepeatAtLeastOnce = "repeat" +{"me"}
OptionalRepeat = "repeat me" *{"or me"}

*/
/*

Identifier attributes can be used in token definitions by prepending a "$" to their name and separate the token name from the attribute by a ".".

Letters = *{Letter}
Letter = "a" | "b" | "c"
LetterCount = $Letter.Len // LetterLen holds the count of Letter token that exist in the document

*/
/*

Special token can be defined by prepending a "$" to their name. Special token must have at least the "type" attribute.

$Number = type: Integer
Arithmetic = $Number "+" $Number

$Id = type: Sequence,
      start: 0,
      step: 2
NextId = $Id.Next // .Next indicates the next value of the sequence

*/
/*

Expressions can be used in the token definitions

Sum = ${1 + 2 + 3} // Sum will be interpreted as 6

SomeIdOrMore = $Id.Existing | ${Id.Existing + 1} // .Existing indicates an available value of the sequence in the whole document

DoubleTheCount = ${Letter.Len + Letter.Len}

*/
/*

Naming convention for tokens
- Token names have to start with a letter
- Token names can only consist of letters, digits and "_"
- Reserved token names are
+-> START - parsing of data will start from this token. Is required for every document.

*/

$Id = type: Sequence,
      start: 2,
      step: 2

START = Header Body

Header = ${Input.Len + Latch.Len + And.Len}, " " // M
         $Input.Len " ",                         // I
         $Latch.Len " " ,                        // L
         $Output.Len " ",                        // O
         $And.Len "\n"                           // A

Body = Inputs,
       Latches,
       Outputs,
       Ands

Inputs = *{Input}
Input  = $Id.Next "\n"

Latches = *{Latch}
Latch   = $Id.Next " " ExistingId "\n"

Outputs = *{Output}
Output  = ExistingId "\n"

Ands = *{And}
And  = $Id.Next " " ExistingId " " ExistingId "\n"

ExistingId = $Id.Existing | ${Id.Existing + 1}
